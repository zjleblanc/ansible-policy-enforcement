# 3. Prevent job execution during maintenance window

Now that we are familiar with the basics of using an OPA policy to prevent job execution in Ansible Automation Platform, let's dive into a common use case: the maintenance window.

For example, if you have a inventory of hosts that should only be updated during non-working hours, let's say from 5PM to 9AM EST, we can use an OPA policy to prevent job execution against that inventory outside of the maintenance window.

Example policy [aap_policy_examples/maintenance_window.rego](aap_policy_examples/maintenance_window.rego):

```rego
package app_policy_examples

# Define maintenance window in UTC
maintenance_start_hour := 12 # 12:00 UTC (5 PM EST)

maintenance_end_hour := 4 # 04:00 UTC (9 AM EST)

# Extract the job creation timestamp (which is in UTC)
created_clock := time.clock(time.parse_rfc3339_ns(input.created)) # returns [hour, minute, second]

created_hour_utc := created_clock[0]

# Check if job was created within the maintenance window (UTC)
is_maintenance_time if {
	created_hour_utc >= maintenance_start_hour # After 12:00 UTC
}

is_maintenance_time if {
	created_hour_utc < maintenance_end_hour # Before 04:00 UTC
}

default maintenance_window := {
	"allowed": true,
	"violations": [],
}

maintenance_window := {
	"allowed": false,
	"violations": ["No job execution allowed outside of maintenance window"],
} if {
	not is_maintenance_time
}
```

Example input provided by Ansible Automation Platform during query:

```json
{
  "id": 785,
  "name": "Demo Job Template",
  "created": "2025-02-27T20:32:14.874821Z",
  "created_by": {
    "id": 1,
    "username": "admin",
    "is_superuser": true
  }
}
```

Example output from policy query:

```rego
{
  "allowed": false,
  "violations": [
    "No job execution allowed outside of maintenance window"
  ]
}
```

When this policy is applied to a specific inventory, all job execution using that inventory will ERROR if it is outside of the maintenance window.

Rego playground: https://play.openpolicyagent.org/p/xuhtchgqR9
